import "scortFFASettings.json";
import "macros.del";

globalvar define MATCH_TIME;
globalvar define RAINBOW_S;
globalvar define playerStats;
globalvar define killStreakHud;
globalvar define specPlayerStats;
globalvar define healOnKill;
globalvar define nerfedHeal;
globalvar define sHeal;
globalvar define heal;
globalvar define matchStarted;
globalvar define playersJoined;
globalvar define endScoreboard;
globalvar define sScoreboard;
playervar define killStreak;

define rainbowColor: CustomColor(128 + 127 * CosineFromDegrees(Time() * RAINBOW_S),
                                 128 + 127 * CosineFromDegrees((Time() * RAINBOW_S) + 120),
                                 128 + 127 * CosineFromDegrees((Time() * RAINBOW_S) + 240), 
                                 250);
define healthColor: CustomColor(r, g, 0, 250);
define percentage: (NormalizedHealth() * 100);
define r: (percentage <= 50) ? 255 : RoundToInteger(256 - (percentage - 50) * 5.12, Rounding.Down);
define g: (percentage >= 50) ? 255 : RoundToInteger(percentage * 5.12, Rounding.Down);


rule: "Created by scort#12211 | Feel free to edit, modify, host your own lobby, whatever; but please don't take credit for the original, that's just mean :(" {}

rule: "Initial Global"
    {
        CreateHudText(all, null, "ㅤTryhard FFA | Created by scort#11221", null, Location.Right, 0, Color.White, rainbowColor, Color.White, HudTextRev.VisibleToSortOrderStringAndColor);

        MATCH_TIME = WorkshopSettingInteger("Settings", "​Match Time (Minutes)", 60, 1, 60, 1) * 60;
        sScoreboard = WorkshopSettingToggle("Settings", "Show Scoreboard at end of match", true, 2);

        healOnKill = WorkshopSettingToggle("Heal On Kill", "Heal On Kill", true, 2);
        sHeal = WorkshopSettingCombo("Heal On Kill", "​Heal Percentage (ex. killing a 200hp hero would heal 100hp if set to 50%)", 1, ["25%", "50%", "75%", "100%"], 4);
        heal = ((sHeal + 1) * 25) / 100;

        killStreakHud = WorkshopSettingToggle("HUD Text", "Show Kill Streaks", true, 1);
        playerStats = WorkshopSettingToggle("HUD Text", "Show Player Stats", true, 2);
        specPlayerStats = WorkshopSettingToggle("HUD Text", "Show Player Stats for spectators", true, 2);
        RAINBOW_S = WorkshopSettingInteger("HUD Text", "​very cool and definitely not gimmicky rainbow cycle speed", 150, 1, 1000, 3);

        SetMatchTime(10);
    }

rule: "Initial Player Join"
    Event.OnPlayerJoin
    {
        playersJoined++;
        killStreak = 0;
    }

rule: "Set Match Time"
    if (IsGameInProgress())
    {
        SetMatchTime(MATCH_TIME);
        DisableCompletion();
        DisableAnnouncer();
    }

rule: "Reset Cooldowns on respawn"
    Event.OngoingPlayer
    if (IsAlive())
    {
        SetAmmo(player, 0, MaxAmmo());
        SetAmmo(player, 1, MaxAmmo());

        // tracer / sym
        SetAbilityCharge(IsHero(Hero.Tracer) || IsHero(Hero.Symmetra) ? player : null, Button.Ability1, 3);

        // junkrat
        SetAbilityCharge(IsHero(Hero.Junkrat) ? player : null, Button.Ability1, 2);

        // dva / bastion / pharah
        SetAbilityResource(player, Button.SecondaryFire, 100);

        SetAbilityCooldown(player, Button.Ability1, 0);
        SetAbilityCooldown(player, Button.Ability2, 0);
        SetAbilityCooldown(player, Button.PrimaryFire, 0);
        SetAbilityCooldown(player, Button.SecondaryFire, 0);
    }

rule: "HUD Text"
    Event.OngoingPlayer
    {
        if (killStreakHud)
        {
            // streak self
            CreateHudText(killStreak >= 5 ? player : null, 
            HeroIconString(HeroOf()), 
            <"<0> (You)", player>, <"Streak: <0>", killStreak>, 
            Location.Left, 0, rainbowColor, Color.White, Color.White, HudTextRev.VisibleToSortOrderStringAndColor, Spectators.VisibleNever);

            // streak enemies
            CreateHudText(killStreak >= 5 ? RemoveFromArray(all, player) : null, 
            HeroIconString(HeroOf()), player, <"Streak: <0>", killStreak>, 
            Location.Left, SlotOf() + 1, healthColor, Color.White, Color.White, HudTextRev.VisibleToSortOrderAndString, Spectators.VisibleNever);
        }

        if (playerStats)
        {
            // stats
            CreateHudText(player, null, null, 
            <"DMG Done: <0> | DMG Taken: <1> | Acc: <2>% | Crit Acc: <3>%", 
            RoundToInteger(PlayerStat(player, PlayerStat.AllDamageDealt), Rounding.Nearest), 
            RoundToInteger(PlayerStat(player, PlayerStat.DamageTaken), Rounding.Nearest), 
            RoundToInteger(PlayerHeroStat(player, HeroOf(), PlayerHeroStat.WeaponAccuracy) * 100, Rounding.Nearest),
            RoundToInteger(PlayerHeroStat(player, HeroOf(), PlayerHeroStat.CriticalHitAccuracy) * 100, Rounding.Nearest)>, 
            Location.Top, 2, Color.White, Color.White, Color.White, HudTextRev.VisibleToSortOrderAndString, Spectators.VisibleNever);
        }

        if (specPlayerStats)
        {
            // specs
            CreateHudText(null, 
            HeroIconString(HeroOf()), 
            <"<0> | <1> Kills | <2> Deaths | <3> Ults | DMG Out: <4> | DMG In: <5> | Acc: <6>% | Crit Acc: <7>%", 
            player, PlayerStat(player, PlayerStat.FinalBlows), PlayerStat(player, PlayerStat.Deaths), PlayerStat(player, PlayerStat.UltimatesUsed), RoundToInteger(PlayerStat(player, PlayerStat.AllDamageDealt), Rounding.Nearest), 
            RoundToInteger(PlayerStat(player, PlayerStat.DamageTaken), Rounding.Nearest), 
            RoundToInteger(PlayerHeroStat(player, HeroOf(), PlayerHeroStat.WeaponAccuracy) * 100, Rounding.Nearest), 
            RoundToInteger(PlayerHeroStat(player, HeroOf(), PlayerHeroStat.CriticalHitAccuracy) * 100, Rounding.Nearest)>,
            IsAlive() ? <"<0> HP<1>", RoundToInteger(Health(), Rounding.Nearest), killStreak >= 5 ? <" | <0> streak", killStreak> : ""> : "Dead", 
            Location.Left, SlotOf(), killStreak >= 5 ? rainbowColor : healthColor, Color.White, healthColor, HudTextRev.VisibleToSortOrderStringAndColor, Spectators.VisibleAlways);
        }
    }

rule: "Update Killstreak "
    Event.OnFinalBlow
    if (IsAlive(Attacker()))
    {
        killStreak++;
    }

rule: "Reset Kills"
    Event.OnDeath
    {
        killStreak = 0;
    }

rule: "Heal On Kill"
    Event.OnFinalBlow
    if (healOnKill && !IsDuplicating(Attacker()) && Health(Attacker()) < MaxHealth(Attacker()))
    {
        Heal(Attacker(), null, heal * MaxHealth(Victim()));
    }

rule: "End Game Scoreboard"
    Event.OngoingPlayer
    if (endScoreboard)
    {
        DestroyAllHudText();
        CreateHudText(all, null, "ㅤTryhard FFA | Created by scort#11221", null, Location.Right, 0, Color.White, rainbowColor, Color.White, HudTextRev.VisibleToSortOrderStringAndColor);
        StartCamera(all, Vector(214.63, 18.51, 48.69), Vector(203.25, 9.50, 68.08));

        SetInvisible(all);
        DisableGameModeHud(all);
        DisableHeroHud(all);
        DisableRespawning(all);
        DisableKillFeed(all);

        MinWait();

        DisallowButton(all, Button.PrimaryFire);
        DisallowButton(all, Button.SecondaryFire);
        DisallowButton(all, Button.Ability1);
        DisallowButton(all, Button.Ability2);
        DisallowButton(all, Button.Ultimate);
        DisallowButton(all, Button.Melee);
        DisallowButton(all, Button.Interact);
        
        CreateHudText(player, null, null, <" \n<0> players joined the lobby in total\n\n\n", playersJoined>, Location.Top, 0);

        CreateHudText(player, HeroIconString(HeroOf()), player,
        <"<0> Kills | <1> Deaths | <2> Ults | DMG Out: <3> | DMG In: <4> | Acc: <5>% | Crit Acc: <6>%", 
        PlayerStat(player, PlayerStat.FinalBlows), PlayerStat(player, PlayerStat.Deaths), PlayerStat(player, PlayerStat.UltimatesUsed), RoundToInteger(PlayerStat(player, PlayerStat.AllDamageDealt), Rounding.Nearest), 
        RoundToInteger(PlayerStat(player, PlayerStat.DamageTaken), Rounding.Nearest), 
        RoundToInteger(PlayerHeroStat(player, HeroOf(), PlayerHeroStat.WeaponAccuracy) * 100, Rounding.Nearest), 
        RoundToInteger(PlayerHeroStat(player, HeroOf(), PlayerHeroStat.CriticalHitAccuracy) * 100, Rounding.Nearest)>, Location.Top, SlotOf() + 1, 
        Color.SkyBlue, Color.SkyBlue);

        CreateHudText(RemoveFromArray(all, player), HeroIconString(HeroOf()), player,
        <"<0> Kills | <1> Deaths | <2> Ults | DMG Out: <3> | DMG In: <4> | Acc: <5>% | Crit Acc: <6>%", 
        PlayerStat(player, PlayerStat.FinalBlows), PlayerStat(player, PlayerStat.Deaths), PlayerStat(player, PlayerStat.UltimatesUsed), RoundToInteger(PlayerStat(player, PlayerStat.AllDamageDealt), Rounding.Nearest), 
        RoundToInteger(PlayerStat(player, PlayerStat.DamageTaken), Rounding.Nearest), 
        RoundToInteger(PlayerHeroStat(player, HeroOf(), PlayerHeroStat.WeaponAccuracy) * 100, Rounding.Nearest), 
        RoundToInteger(PlayerHeroStat(player, HeroOf(), PlayerHeroStat.CriticalHitAccuracy) * 100, Rounding.Nearest)>, Location.Top, SlotOf() + 1, 
        Color.Red, Color.Red);
    }

rule: "End Game"
    if (MatchTime() <= 0)
    {
        WaitIfTrue(0.08);
        if (sScoreboard)
        {
            endScoreboard = true;
            Wait(15);
        }

        EnableCompletion();
    }

rule: "Force Respawn"
    Event.OngoingPlayer
    if (IsButtonHeld(player, Button.Jump) && IsDead())
    {
        Respawn();
    }

rule: "[Debug] :: Bots"
    Event.OngoingPlayer
    if (player == host && IsInteractHeld() && !IsCrouchHeld())
    {
        CreateDummyBot(HeroOf(), OppositeTeamOf(TeamOf()), -1, RayCastHitPosition(EyePosition(), EyePosition() + Facing() * 100), Facing() * -1);
    }

rule: "[Debug] :: Destroy Bots"
    Event.OngoingPlayer
    if (player == host && IsCrouchHeld() && IsInteractHeld())
    {
        DestroyAllDummyBots();
    }

rule: "[Debug] :: Damage"
    Event.OngoingPlayer
    if (player == host && IsCrouchHeld())
    {
        Damage(player, null, 10);
    }

rule: "[Debug] :: Hud"
    Event.OngoingPlayer
    if (player == host)
    {
        CreateHudText(player, null, null, <" \nPos: <0>", PositionOf()>, Location.Top, 10);
        CreateHudText(player, null, null, <"Facing: <0>", FacingDirectionOf()>, Location.Top, 11);
        CreateHudText(player, null, null, <"Raycast: <0>", RayCastHitPosition(EyePosition(), EyePosition() + Facing() * 100)>, Location.Top, 12);
    }