import "macros.del";
// import "debugtools.del";
// import "cancer.del";

define verText: "Ver 1.6.0";

// bhop
playervar define bhopSpeed;
playervar define speedAccumInAir;
playervar define isChasing;

// primary
playervar define primaryHit;
playervar define primaryStartPos;

// secondary
playervar define secondaryHit;
playervar define secondaryEyePos;
playervar define secondaryOrb;
playervar define secondaryStartPos;

// wallbang
playervar define isFiring;

// grapple
playervar define grappleStartPos;
playervar define grappleEyePos;
playervar define grappleBeamEffect;
playervar define usingGrapple;
playervar define grappleHit;
playervar define grappleHitSphere;
playervar define grapplePlayerBeamEffect;
playervar define grappleHitPlayer;
playervar define grappleHitPlayerSphere;

// misc
playervar define effect;
playervar define damageText;
playervar define textChase;
playervar define livingEnemies;

// constants :: bhop
globalvar define DEFAULT_START_SPEED;
globalvar define BHOP_MAX_SPEED;
globalvar define BHOP_SPEED_RATE;

// constants :: wallbang
globalvar define NUM_WALLBANGS;

// constants :: grapple
globalvar define GRAPPLE_PULL_SPEED;

// constants :: secondary
globalvar define SECONDARY_PROJECTILE_SPEED;
globalvar define SECONDARY_RADIUS;

// constants: misc
globalvar define MATCH_TIME_LIMIT;

// toggle
playervar define wallbangEnabled;
globalvar define matchStarted;

// buffs
globalvar define NUM_BOUNCES;

rule: "Debug Hud"
    Event.OngoingPlayer
    {
        CreateHudText(player, null, null, PositionOf(), Location.Top, -1);
    }

rule: "Initial Global"
    {
        cdText = [];
        buffEffect = [];
        buffPos = [];
        buffIcon = [];
        matchStarted = false;
        DEFAULT_START_SPEED = WorkshopSettingInteger("BHop", "1. Starting Speed (Default 100)", 100, 25, 300);
        BHOP_MAX_SPEED = WorkshopSettingInteger("BHop", "2. Maximum Speed From Bunny Hops (Default 320)", 320, 25, 10000);
        BHOP_SPEED_RATE = WorkshopSettingReal("BHop", "3. Bunny Speed Increments (Default 12.5)", 12.5, 1, 100);

        NUM_WALLBANGS = WorkshopSettingInteger("Buffs", "1. Wallbang Amount (Default 4)", 4, 2, 100);
        WALLBANG_CD = WorkshopSettingInteger("Buffs", "2. Wallbang Orb Spawn Time", 45, 1, 300);
        WALLBANG_DUR = WorkshopSettingInteger("Buffs", "3. Wallbang Duration", 15, 1, 300);
        NUM_BOUNCES = WorkshopSettingInteger("Buffs", "4. Bullet Bounces (12 players = 5 max! Set this too high and buffs/bullet trails won't appear!)", 5, 1, 64);
        BOUNCE_CD = WorkshopSettingInteger("Buffs", "5. Bounce Bullets Orb Spawn Time", 75, 1, 300);
        BOUNCE_DUR = WorkshopSettingInteger("Buffs", "6. Bounce Bullets Duration", 15, 1, 300);
        LIFE_CD = WorkshopSettingInteger("Buffs", "Extra Life Orb Spawn Time", 90, 1, 300);

        SECONDARY_PROJECTILE_SPEED = WorkshopSettingInteger("Secondary Fire", "1. Secondary Projectile Speed (Default 110)", 110, 10, 500);
        SECONDARY_RADIUS = WorkshopSettingReal("Secondary Fire", "2. Secondary Explosion Radius (Default 4.5)", 4.5, 2, 10);

        MATCH_TIME_LIMIT = WorkshopSettingInteger("Misc", "2. Match Time (Minutes) (Default 20)", 20, 1, 60) * 60;
        GRAPPLE_PULL_SPEED = WorkshopSettingReal("Misc", "3. Grapple Pull Speed (Default 3.5)", 3.5, 0.1, 20);
    }
rule: "Initial Player Join"
    Event.OnPlayerJoin
    {
        hudText = [];
    }

rule: "Init"
    if (!matchStarted && CountOf(AllLivingPlayers()) > 0)
    {
        DisableInspectorRecording();
        DisableCompletion();
        DisableAnnouncer();

        ServerLoadText();
        CreateHudText(all, null, verText, null, Location.Left, -1);
        CreditText();

        SetMatchTime(15);
        Wait(16);
        matchStarted = true;
        SetMatchTime(MATCH_TIME_LIMIT);
        LifeCooldown();
        WallbangCooldown();
        BounceCooldown();
    }

rule: "End Game"
    if (matchStarted)
    {
        Wait(MATCH_TIME_LIMIT);
        EnableCompletion();
    }

rule: "Hit Text"
    Event.OnDamageDealt
    {
        textChase = Vector(0,1,0);
        if (EventWasCriticalHit())
        {
            CreateInWorldText(player, "Critical", PositionOf(Victim()) + textChase, 1.2, Clipping.DoNotClip, InworldTextRev.VisibleToPositionAndString, Color.Red);
            PlayEffect(player, PlayEffect.BuffImpactSound, Color.White, player, 100);
        }
        else
        {
            CreateInWorldText(player, "Hit", PositionOf(Victim()) + textChase, 1.2, Clipping.DoNotClip, InworldTextRev.VisibleToPositionAndString, Color.Yellow);
        }

        damageText = LastTextID();
        ChaseVariableAtRate(textChase, Vector(0,1.5,0), 2.5, RateChaseReevaluation.DestinationAndRate);
        Wait(0.4);
        StopChasingVariable(textChase);
        DestroyInWorldText(damageText);
    }

rule: "Primary effect"
    Event.OngoingPlayer
    if (!wallbangEnabled && !bounceEnabled && IsFiringPrimary())
    {
        livingEnemies = FilteredArray(EnemyPlayers(), IsAlive(Curr()));
        primaryStartPos = WorldVectorOf(Right() - Vector(0, 0.3, 0), player) * 0.18;
        primaryHit = RayCastHitPosition(EyePosition(), EyePosition() + Facing() * 100, livingEnemies, player);
        CreateBeamEffect(all, BeamType.BadBeam, EyePosition() + Facing() + primaryStartPos, primaryHit, Color.Aqua, EffectRev.None);
        effect = LastCreatedEntity();
        
        PlayEffect(all, PlayEffect.GoodExplosion, Color.Aqua, EyePosition() + Facing() + primaryStartPos, 0.2);
        PlayEffect(all, PlayEffect.GoodExplosion, Color.Aqua, primaryHit, 0.2);

        Wait(0.25);
        DestroyEffect(effect[0]);
        DestroyEffect(effect[1]);
    }

disabled rule: "---------------- Bhop ----------------"{}
rule: "[Bhop] :: Auto Hop"
    Event.OngoingPlayer
    if (IsOnGround() && IsJumpHeld())
    {
        DisableJump();
        MinWait();
        AllowJump();
    }

rule: "[Bhop] :: Starting Speed"
    Event.OngoingPlayer
    {
        bhopSpeed = DEFAULT_START_SPEED;
    }

rule: "[Bhop] :: If Airborne"
    Event.OngoingPlayer
    if ((!usingGrapple && IsJumping() && HorizontalSpeedOf() > DEFAULT_START_SPEED / 33.33) || (usingGrapple && IsInAir() && DistanceBetween(grappleEyePos, grappleHit) <= 100))
    {
        speedAccumInAir = HorizontalSpeedOf();
        Wait(0.05);
        ChaseVariableAtRate(bhopSpeed, BHOP_MAX_SPEED, BHOP_SPEED_RATE, RateChaseReevaluation.DestinationAndRate);

        isChasing = true;
    }

rule: "[Bhop] :: Update Speed"
    Event.OngoingPlayer
    if (isChasing)
    {
        speedAccumInAir = HorizontalSpeedOf();
        SetMoveSpeed(player, bhopSpeed);
        WaitIfTrue(0.032);
        Loop();
    }

rule: "[Bhop] :: Update isChasing"
    Event.OngoingPlayer
    if (isChasing && bhopSpeed == BHOP_MAX_SPEED || bhopSpeed == DEFAULT_START_SPEED)
    {
        StopChasingVariable(bhopSpeed);
        isChasing = false;
    }

rule: "[Bhop] :: Reset speed if turning too fast"
    Event.OngoingPlayer
    if (HorizontalSpeedOf() < speedAccumInAir - 0.45)
    {
        StopChasingVariable(bhopSpeed);
        isChasing = false;
        bhopSpeed = DEFAULT_START_SPEED;
        SetMoveSpeed(player, bhopSpeed);
    }

rule: "[Bhop] :: Stop chasing if too slow"
    Event.OngoingPlayer
    if (!usingGrapple && isChasing && HorizontalSpeedOf() <= DEFAULT_START_SPEED / 66.66)
    {
        StopChasingVariable(bhopSpeed);
        isChasing = false;
        bhopSpeed = DEFAULT_START_SPEED;
        SetMoveSpeed(player, bhopSpeed);
        WaitIfTrue(0.032);
        Loop();
    }

rule: "[Bhop] :: Stop if player is on ground"
    Event.OngoingPlayer
    if ((IsOnGround() && !usingGrapple) && (bhopSpeed > DEFAULT_START_SPEED))
    {
        WaitIfTrue(0.2);
        StopChasingVariable(bhopSpeed);
        bhopSpeed = DEFAULT_START_SPEED;
        // ChaseVariableAtRate(bhopSpeed, DEFAULT_START_SPEED, 325, RateChaseReevaluation.DestinationAndRate);
        // isChasing = true;
        SetMoveSpeed(player, bhopSpeed);
    }

disabled rule: "---------------- Secondary Fire ----------------"{}

rule: "[Secondary Fire] :: Projectile"
    Event.OngoingPlayer
    if (matchStarted && IsSecondaryHeld() && IsAlive())
    {
        // todo: projectile will not hit a target if said target does not interrupt the projectile's path, need jacob's help
        livingEnemies = FilteredArray(EnemyPlayers(), IsAlive(Curr()));
        secondaryStartPos = WorldVectorOf(Right() - Vector(0, 0.3, 0), player) * 0.18;
        secondaryHit = RayCastHitPosition(EyePosition(), EyePosition() + Facing() * 100, livingEnemies, player);
        secondaryEyePos = EyePosition() + secondaryStartPos;
        DestroyEffect(secondaryOrb);
        PlayEffect(all, PlayEffect.RingExplosionSound, Color.White, player, 50);
        PlayEffect(all, PlayEffect.GoodExplosion, Color.Red, EyePosition() + Facing() + secondaryStartPos, 0.2);

        // projectile
        CreateEffect(all, Effect.Orb, Color.Red, secondaryEyePos, 0.1);
        secondaryOrb = LastCreatedEntity();
        ChaseVariableAtRate(secondaryEyePos, secondaryHit, SECONDARY_PROJECTILE_SPEED, RateChaseReevaluation.DestinationAndRate);

        Wait(0.5);
    }

rule: "[Secondary Fire] :: Impulse"
    Event.OngoingPlayer
    if (DistanceBetween(secondaryEyePos, secondaryHit) < 0.1 || RayCastHitPlayer(EyePosition(), secondaryEyePos, AllLivingPlayers(), player) != null)
    {
        StopChasingVariable(secondaryEyePos);
        define players = FilteredArray(AllLivingPlayers(), DistanceBetween(Curr(), secondaryEyePos) <= SECONDARY_RADIUS);
        for (define loop = 0; CountOf(players); 1)
        {
            ApplyImpulse(players[loop], DirectionTowards(secondaryEyePos, players[loop] + Vector(0, YOf(EyePosition(players[loop])- players[loop]) / 2, 0) + Up()), 15, Relative.ToWorld, ContraryMotion.Incorporate);
        }
        
        // projectile hit effect
        PlayEffect(all, PlayEffect.GoodExplosion, Color.Red, secondaryHit, SECONDARY_RADIUS - 2);
        PlayEffect(VisibleTo: all, Type: PlayEffect.RingExplosionSound, Position: player, Radius: 15);
        DestroyEffect(secondaryOrb);
    }

disabled rule: "---------------- Grapple ----------------"{}
rule: "[Grapple] :: Grapple Start"
    Event.OngoingPlayer
    if (matchStarted && IsAbility2Held() && IsAlive())
    {
        livingEnemies = FilteredArray(EnemyPlayers(), IsAlive(Curr()));
        grappleHit = RayCastHitPosition(EyePosition(), EyePosition() + Facing() * 100, livingEnemies, player);
        grappleHitPlayer = RayCastHitPlayer(EyePosition(), EyePosition() + Facing() * 100, livingEnemies, player, true);
        grappleEyePos = EyePosition();

        if (grappleHitPlayer)
        {
            CreateBeamEffect(all, BeamType.GrappleBeam, player, grappleHitPlayer, Color.Yellow, EffectRev.VisibleToPositionAndRadius);
            grappleBeamEffect = LastCreatedEntity();
            
            CreateEffect(all, Effect.Sphere, Color.Yellow, grappleHitPlayer, 0.2);
            grappleHitPlayerSphere = LastCreatedEntity();
        }
        else 
        {
            CreateBeamEffect(all, BeamType.GrappleBeam, player, grappleHit, Color.Yellow, EffectRev.VisibleToPositionAndRadius);
            grappleBeamEffect = LastCreatedEntity();
            
            CreateEffect(all, Effect.Sphere, Color.Yellow, grappleHit, 0.2);
            grappleHitSphere = LastCreatedEntity();
        }

        PlayEffect(all, PlayEffect.BadExplosion, Color.Yellow, player, 0.25);
        PlayEffect(all, PlayEffect.DebuffImpactSound, Color.Yellow, player, 100);
        PlayEffect(all, PlayEffect.BadExplosion, Color.Yellow, grappleHit, 0.2);
        PlayEffect(all, PlayEffect.DebuffImpactSound, Color.Yellow, grappleHit, 50);

        usingGrapple = true;
    }

rule: "[Grapple] :: Pull"
    Event.OngoingPlayer
    if (usingGrapple && DistanceBetween(grappleEyePos, grappleHit) <= 100)
    {
        if (grappleHitPlayer && IsAlive(grappleHitPlayer))
            ApplyImpulse(grappleHitPlayer, Vector(0, 0.3, 0) + DirectionTowards(grappleHitPlayer, EyePosition()), GRAPPLE_PULL_SPEED / 1.25, Relative.ToWorld, ContraryMotion.Incorporate);
        else
            ApplyImpulse(player, Vector(0, 0.3, 0) + DirectionTowards(EyePosition(), grappleHit), GRAPPLE_PULL_SPEED, Relative.ToWorld, ContraryMotion.Incorporate);

        Wait(0.064);
        LoopIfConditionIsTrue();
    }

rule: "[Grapple] :: Slow Directional Movement"
    Event.OngoingPlayer
    if (usingGrapple && DistanceBetween(grappleEyePos, grappleHit) <= 100)
    {
        ForceThrottle(player, 0, 0.05, 0, 0.05, 0, 0.05);
    }

rule: "[Grapple] :: Grapple End"
    Event.OngoingPlayer
    if ((!IsAbility2Held() || IsDead() || IsDead(grappleHitPlayer)) || (!IsInLineOfSight(player, grappleHitPlayer) && grappleHitPlayer))
    {
        usingGrapple = false;
    }

rule: "[Grapple] :: Destroy Effects"
    Event.OngoingPlayer
    if (!usingGrapple)
    {
        DestroyEffect(grappleBeamEffect);
        DestroyEffect(grapplePlayerBeamEffect);
        DestroyEffect(grappleHitSphere);
        DestroyEffect(grappleHitPlayerSphere);
        StopForcingThrottle();
    }

disabled rule: "---------------- Buff ----------------"{}

        /*
        [0]: wallbang
        [1]: extra 'life'
        [2]: bullet bouncy bouncy go go
        [3]: something (walls?)
        */

globalvar define buffEffect;
globalvar define buffPos;
globalvar define buffIcon;
playervar define hudText;
globalvar define cdText;
globalvar define wallbangCD;
globalvar define WALLBANG_CD;
playervar define wallbangDur;
globalvar define WALLBANG_DUR;
globalvar define lifeCD;
globalvar define LIFE_CD;
playervar define extraLife;
playervar define extraLifeIcon;
globalvar define bounceCD;
globalvar define BOUNCE_CD;
playervar define bounceEnabled;
playervar define bounceDur;
globalvar define BOUNCE_DUR;

void WallbangCooldown() globalvar "Wallbang Cooldown Sub"
    {
        wallbangCD = WALLBANG_CD;
        ChaseVariableAtRate(wallbangCD, 0, 1, RateChaseReevaluation.None);
        CreateInWorldText(all, <"<0>", RoundToInteger(wallbangCD, Rounding.Up)>, buffPos[0], 2, Clipping.ClipAgainstSurfaces);
        cdText[0] = LastTextID();
    }

void LifeCooldown() globalvar "Life Cooldown Sub"
    {
        lifeCD = LIFE_CD;
        ChaseVariableAtRate(lifeCD, 0, 1, RateChaseReevaluation.None);
        CreateInWorldText(all, <"<0>", RoundToInteger(lifeCD, Rounding.Up)>, buffPos[1], 2, Clipping.ClipAgainstSurfaces);
        cdText[1] = LastTextID();
    }

void BounceCooldown() globalvar "Bounce Cooldown Sub"
    {
        bounceCD = BOUNCE_CD;
        ChaseVariableAtRate(bounceCD, 0, 1, RateChaseReevaluation.None);
        CreateInWorldText(all, <"<0>", RoundToInteger(bounceCD, Rounding.Up)>, buffPos[2], 2, Clipping.ClipAgainstSurfaces);
        cdText[2] = LastTextID();
    }

rule: "[Buff] :: Locations Switch"
    {
        switch (true)
        {
            case (CurrentMap() == Map.Workshop_Chamber || CurrentMap() == Map.Workshop_Expanse || CurrentMap() == Map.Workshop_Expanse_Night || CurrentMap() == Map.Workshop_Island || CurrentMap() == Map.Workshop_Island_Night):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Blizzard_World || CurrentMap() == Map.Blizzard_World_Winter):
                buffPos[0] = Vector(-24.51, 1, 58.27);
                buffPos[1] = Vector(-71.64, 2.75, 155.42);
                buffPos[2] = Vector(-79.53, 1.25, 63.92);
                buffPos[3] = Vector(-37.51, 0.79, -6);
                break;
            case (CurrentMap() == Map.Chateau_Guillard):
                buffPos[0] = Vector(206.21, 16.50, 87.00);
                buffPos[1] = Vector(196.35, 17.50, 47.06);
                buffPos[2] = Vector(206.15, 1, 77.42);
                buffPos[3] = Vector(180.60, 10.00, 101.93);
                break;
            case (CurrentMap() == Map.Dorado):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Ecopoint_Antarctica || CurrentMap() == Map.Ecopoint_Antarctica_Winter):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Eichenwalde):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Hanamura || CurrentMap() == Map.Hanamura_Winter):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Hollywood):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Ilios_Lighthouse):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Ilios_Ruins):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Ilios_Well):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Kings_Row || CurrentMap() == Map.Kings_Row_Winter):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Lijiang_Control_Center || CurrentMap() == Map.Lijiang_Control_Center_Lunar):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Lijiang_Garden || CurrentMap() == Map.Lijiang_Garden_Lunar):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Lijiang_Night_Market || CurrentMap() == Map.Lijiang_Night_Market_Lunar):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Necropolis):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Nepal_Sanctum):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Nepal_Shrine):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Nepal_Village):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Oasis_City_Center):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Oasis_Gardens):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Oasis_University):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Paris):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Temple_of_Anubis):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
            case (CurrentMap() == Map.Volskaya_Industries):
                buffPos[0] = Vector(0, 1, 2);
                buffPos[1] = Vector(0, 1, 6);
                buffPos[2] = Vector(0, 1, -2);
                buffPos[3] = Vector(0, 1, -6);
                break;
        }
    }

rule: "[Buff] :: Wallbang : Orb"
    if (wallbangCD == 0 && matchStarted)
    {
        PlayEffect(all, PlayEffect.GoodPickupEffect, Color.Yellow, buffPos[0], 1);
        CreateEffect(all, Effect.Orb, Color.Orange, buffPos[0], 2);
        buffEffect[0] = LastCreatedEntity();
        CreateInWorldText(all, AbilityIconString(Hero.Baptiste, ButtonValue(Button.Ultimate)), buffPos[0], 2, Clipping.ClipAgainstSurfaces);
        buffIcon[0] = LastTextID();
        DestroyInWorldText(cdText[0]);
    }

rule: "[Buff] :: Wallbang : Collect"
    Event.OngoingPlayer
    if (matchStarted && !bounceEnabled && EntityExists(buffEffect[0]) && DistanceBetween(buffPos[0], player) < 2.5 && HasSpawned() && IsAlive())
    {
        wallbangEnabled = true;
        bounceEnabled = false;
        DestroyInWorldText(cdText[0]);
        DestroyInWorldText(buffIcon[0]);
        DestroyEffect(buffEffect[0]);
        PlayEffect(all, PlayEffect.GoodPickupEffect, Color.Yellow, buffPos[0], 1);
        PlayEffect(all, PlayEffect.BuffExplosionSound, Color.Yellow, buffPos[0], 200);

        BigMessage(player, <"<0> Wallbang <0>", AbilityIconString(Hero.Baptiste, ButtonValue(Button.Ultimate))>);

        WallbangCooldown(); 

        wallbangDur = WALLBANG_DUR;
        ChaseVariableAtRate(wallbangDur, 0, 1, RateChaseReevaluation.None)
        CreateHudText(player, null, null, <"<1> Wallbang: <0> <1>", RoundToInteger(wallbangDur, Rounding.Up), AbilityIconString(Hero.Baptiste, ButtonValue(Button.Ultimate))>, Location.Top, -1);
        hudText[0] = LastTextID();
        SmallMessage(player, <"Your bullets go through up to <0> walls!", NUM_WALLBANGS>);

        Wait(WALLBANG_DUR);
        DestroyHudText(hudText[0]);
        wallbangEnabled = false;
    }

rule: "[Buff] :: Wallbang : Action"
    Event.OngoingPlayer
    if (wallbangEnabled && IsFiringPrimary())
    {
        define gunPos = EyePosition() + Facing() + WorldVectorOf(Right() - Vector(0, 0.3, 0), player) * 0.18;
        define wallbangHitPos = EyePosition();

        define wallbangLivingEnemies;
        define wallbangHitPlayer;
        define wallbangCount = 0;
        while (DistanceBetween(gunPos, wallbangHitPos) < 100 && wallbangCount < NUM_WALLBANGS)
        {
            wallbangLivingEnemies = FilteredArray(EnemyPlayers(), IsAlive(Curr()));
            wallbangHitPos = RayCastHitPosition(wallbangHitPos + Facing(), wallbangHitPos + Facing() * (100 - DistanceBetween(gunPos, wallbangHitPos)), wallbangLivingEnemies, player, true);
            PlayEffect(all, PlayEffect.GoodExplosion, Color.Yellow, wallbangHitPos, 0.2);
            wallbangHitPlayer = RayCastHitPlayer(wallbangHitPos, wallbangHitPos + Facing() * (100 - DistanceBetween(gunPos, wallbangHitPos)), wallbangLivingEnemies, player, true);
            if (DistanceBetween(wallbangHitPos, EyePosition(wallbangHitPlayer) + Vector(0,0.2,0)) < 0.3 && wallbangHitPlayer != null)
            {
                PlayEffect(player, PlayEffect.BuffImpactSound, Color.White, player, 100);
                Damage(wallbangHitPlayer, player, 10000);
            }
            else
            {
                Damage(wallbangHitPlayer, player, 10000);
            }

            wallbangCount++;
        }

        CreateBeamEffect(all, BeamType.BadBeam, gunPos, wallbangHitPos, Color.Yellow, EffectRev.None);
        effect[0] = LastCreatedEntity();
        PlayEffect(all, PlayEffect.GoodExplosion, Color.Yellow, gunPos, 0.2);

        Wait(0.25);
        DestroyEffect(effect[0]);
    }

rule: "[Buff] :: Extra Life : Orb"
    if (lifeCD == 0 && matchStarted)
    {
        PlayEffect(all, PlayEffect.GoodPickupEffect, Color.SkyBlue, buffPos[1], 1);
        CreateEffect(all, Effect.Orb, Color.SkyBlue, buffPos[1], 2);
        buffEffect[1] = LastCreatedEntity();
        CreateInWorldText(all, IconString(Icon.Heart), buffPos[1], 2, Clipping.ClipAgainstSurfaces);
        buffIcon[1] = LastTextID();
        DestroyInWorldText(cdText[1]);
    }

rule: "[Buff] :: Extra Life : Collect"
    Event.OngoingPlayer
    if (matchStarted && !extraLife && EntityExists(buffEffect[1]) && DistanceBetween(buffPos[1], player) < 2.5 && HasSpawned() && IsAlive())
    {
        extraLife = true;
        SetStatus(player, null, Status.Unkillable, 10000);

        DestroyInWorldText(cdText[1]);
        DestroyInWorldText(buffIcon[1]); 
        DestroyEffect(buffEffect[1]); // destroy visual effect at buffPos 1
        PlayEffect(all, PlayEffect.GoodPickupEffect, Color.SkyBlue, buffPos[1], 1); // visual effect at buffPos 1
        PlayEffect(all, PlayEffect.BuffExplosionSound, Color.SkyBlue, buffPos[1], 200); // sound effect at buffPos 1

        BigMessage(player, <"<0> Extra Life <0>", IconString(Icon.Heart)>); // buff toast

        LifeCooldown(); // start cooldown

        CreateHudText(player, null, null, <"<0> Extra Life <0>", IconString(Icon.Heart)>, Location.Top, -1);
        hudText[1] = LastTextID();
        SmallMessage(player, "Extra life");
        CreateInWorldText(all, IconString(Icon.Heart), player, 1, Clipping.ClipAgainstSurfaces, InworldTextRev.VisibleToPositionAndString, Color.Yellow);
        extraLifeIcon = LastTextID();
    }

rule: "[Buff] :: Extra Life : Remove"
    Event.OngoingPlayer
    if (extraLife && (Health() == 1 || IsDead()))
    {
        extraLife = false;
        ClearStatus(player, Status.Unkillable);
        SmallMessage(player, "Extra life used");
        PlayEffect(player, PlayEffect.DebuffImpactSound, Color.White, player, 200);

        Wait(0.25);
        SetPlayerHealth(player, MaxHealth());
        DestroyInWorldText(extraLifeIcon);
        DestroyHudText(hudText[1]);
    }

rule: "[Buff] :: Bouncey Bullet : Orb"
    if (bounceCD == 0 && matchStarted)
    {
        PlayEffect(all, PlayEffect.GoodPickupEffect, Color.Purple, buffPos[2], 1);
        CreateEffect(all, Effect.Orb, Color.Purple, buffPos[2], 2);
        buffEffect[2] = LastCreatedEntity();
        CreateInWorldText(all, AbilityIconString(Hero.Winston, ButtonValue(Button.Ability1)), buffPos[2], 2, Clipping.ClipAgainstSurfaces);
        buffIcon[2] = LastTextID();
        DestroyInWorldText(cdText[2]);
    }

rule: "[Buff] :: Bouncey Bullet : Collect"
    Event.OngoingPlayer
    if (matchStarted && !wallbangEnabled && EntityExists(buffEffect[2]) && DistanceBetween(buffPos[2], player) < 2.5 && HasSpawned() && IsAlive())
    {
        bounceEnabled = true;
        wallbangEnabled = false;

        DestroyInWorldText(cdText[2]);
        DestroyInWorldText(buffIcon[2]);
        DestroyEffect(buffEffect[2]);
        PlayEffect(all, PlayEffect.GoodPickupEffect, Color.Purple, buffPos[2], 1);
        PlayEffect(all, PlayEffect.BuffExplosionSound, Color.Purple, buffPos[2], 200);

        BigMessage(player, <"<0> Bounce Bullets <0>", AbilityIconString(Hero.Winston, ButtonValue(Button.Ability1))>);

        BounceCooldown();

        bounceDur = BOUNCE_DUR;
        ChaseVariableAtRate(bounceDur, 0, 1, RateChaseReevaluation.None)
        CreateHudText(player, null, null, <"<1> Bounce Bullets: <0> <1>", RoundToInteger(bounceDur, Rounding.Up), AbilityIconString(Hero.Winston, ButtonValue(Button.Ability1))>, Location.Top, -1);
        hudText[2] = LastTextID();
        SmallMessage(player, "Your bullets bounce!");

        Wait(BOUNCE_DUR);
        DestroyHudText(hudText[2]);

        bounceEnabled = false;
    }

playervar define normals;
playervar define hitPos;
playervar define hitPosPlayer;
playervar define ricochets;
playervar define hitPosBounces;
playervar define hitPosPlayerBounces;
playervar define bounceEffect;

playervar define eyePos;
playervar define facing;

playervar define effectArr;
rule: "[Buff] :: Bouncey Bullet : Action"
    Event.OngoingPlayer
    if (bounceEnabled && IsFiringPrimary())
    {
        normals = [];
        hitPos = [];
        hitPosPlayer = [];
        ricochets = [];
        hitPosBounces = [];
        hitPosPlayerBounces = [];

        for (define loop = 0; loop < NUM_BOUNCES; loop++)
        {
            if (loop > 0)
            {
                eyePos = hitPos[loop - 1];
                facing = ricochets[loop - 1];
            }
            else
            {
                eyePos = EyePosition() + Facing() + WorldVectorOf(Right() - Vector(0, 0.3, 0), player) * 0.18;
                facing = FacingDirectionOf();
            }

            normals[loop] = RayCastHitNormal(eyePos, eyePos + facing * 100, all, player, true);
            hitPos[loop] = RayCastHitPosition(eyePos, eyePos + facing * 100, all, player, true);
            hitPosPlayer[loop] = RayCastHitPlayer(eyePos, eyePos + facing * 100, all, player, true);
            ricochets[loop] = facing - (2 * (normals[loop] * DotProduct(facing, normals[loop])));
            hitPosBounces[loop] = RayCastHitPosition(hitPos[loop], hitPos[loop] + ricochets[loop] * 100, AllLivingPlayers(), player, true);
            hitPosPlayerBounces[loop] = RayCastHitPlayer(hitPos[loop], hitPos[loop] + ricochets[loop] * 100, AllLivingPlayers(), player, true);

            // todo: destroy beam effects created by the player after .25s
            CreateBeamEffect(all, BeamType.BadBeam, eyePos, hitPos[loop], Color.Purple, EffectRev.None);
            CreateBeamEffect(all, BeamType.BadBeam, hitPos[loop], hitPosBounces[loop], Color.Purple, EffectRev.None);
            
            PlayEffect(all, PlayEffect.GoodExplosion, Color.Purple, hitPosBounces[loop], 0.2);
            PlayEffect(all, PlayEffect.GoodExplosion, Color.Purple, hitPos[loop], 0.2);
            PlayEffect(all, PlayEffect.GoodExplosion, Color.Purple, eyePos, 0.2);

            Damage(hitPosPlayer[loop], player, 10000);
            Damage(hitPosPlayerBounces[loop], player, 10000);
        }
    }

rule: "[Buff] :: Wallhack : Orb"
    {
        PlayEffect(all, PlayEffect.GoodPickupEffect, Color.Red, buffPos[3], 1);
        CreateEffect(all, Effect.Orb, Color.Red, buffPos[3], 2);
        buffEffect[3] = LastCreatedEntity();
        CreateInWorldText(all, IconString(Icon.Eye), buffPos[3], 2, Clipping.ClipAgainstSurfaces);
        buffIcon[3] = LastTextID();
        DestroyInWorldText(cdText[3]);
    }
